#Taken from TI yocto BSP Layer Version 05-2014, original name:ti33xx.inc
SOC_FAMILY = "ti33x"
require conf/machine/include/soc-family.inc
DEFAULTTUNE ?= "cortexa8thf-neon"
require conf/machine/include/tune-cortexa8.inc
# List common SoC features
MACHINE_FEATURES = "kernel26 usbgadget usbhost vfat ext2 screen alsa ethernet can"

# Sub-architecture support adapted fom fsl-arm
MACHINE_SOCARCH = "${TUNE_PKGARCH}-ti33x"
PACKAGE_EXTRA_ARCHS_append = " ${MACHINE_SOCARCH}"

# Bootloader
PREFERRED_PROVIDER_virtual/bootloader ?= "ccu-bootloader"
PREFERRED_PROVIDER_virtual/prebootloader ?= "ccu-bootloader-ipl"
INTREE_DEFCONFIG_pn-ccu-bootloader = "am335x_defconfig"
INTREE_DEFCONFIG_pn-ccu-bootloader-ipl = "am335x_mlo_defconfig"
BAREBOX_IPL_IMAGE_BASE_NAME = "MLO-${MACHINE}-${PKGV}-${PKGR}-${DATETIME}"
BAREBOX_IPL_BIN_SYMLINK = "MLO"
# all am335x need an mlo, whenever we need a bootloader
# we need to have a dependency to build the mlo too
DEPENDS_pn-ccu-bootloader_append = " virtual/prebootloader"
DEPENDS_pn-u-boot_append = " virtual/prebootloader"

# Kernel 
PREFERRED_PROVIDER_virtual/kernel ?= "ccu-kernel"
KERNEL_IMAGETYPE ?= "zImage"
INTREE_DEFCONFIG_pn-ccu-kernel = "am335x_phytec_defconfig"
# Install modules in initramfs and rootfs
PACKAGE_INSTALL_append = " kernel-modules"

SERIAL_CONSOLES ?= "115200;ttyO0"
# dont install kernelimage to rootfs
RDEPENDS_kernel-base = ""
# dont create legacy tarball of modules
MODULE_TARBALL_DEPLOY = "0"
# powermanagement firmware for the on-die Cortex M3
# the newer version makes a lot of problems dont include
# per default
#RDEPENDS_kernel-modules_append = " amx3-cm3"
